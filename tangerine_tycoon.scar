(* 
http://github.com/georgri/Tangerine-Tycoon-stockmarket-bot

A simple bot for automated trading in The Tangerine Tycoon stock market
minigame.


TO STOP THE BOT IN CASE OF EMERGENCY: Ctrl+Alt+S


General description
===============================
The bot buys commodity when it goes below 30 tangerines for 50%
of the available money and then sells it when it goes above 80. 
The bot behaviour is not smart nor optimal. Playing manually will benefit you
more quickly. The bot is useful when you are away for several hours and your
computer is always on.   

Requirements
===============================
The bot is only tested to work properly 
- for The Tangerine Tycoon v1.26 
- in Mozilla Firefox (not compatible with Chrome) 
- with page scale set to 100% (not compatible with other scales)
- with flash player quality set to "high" (not compatible with other quality settings)
- under Windows 7 (wherever SCAR Divi 3.40 is working) 

Instruction
===============================
1. Download and install SCAR Divi 3.40 
   (download: http://www.scar-divi.com/download.php?file=scar-3.40.00-setup.exe)
2. Open this script (tangerine_tycoon.scar) in SCAR
3. Open the Tangerine Tycoon in Firefox browser (Chrome will not work)
   (kongregate: http://www.kongregate.com/games/GazThomas/tangerine-tycoon)
  - Page scale should be 100%, set it properly by ctrl + mouse wheel!
  - You should have at least 50 tangerines! Earn them right now if you don't!
4. Go to The Tangerine Exchange screen (press right arrow on the main screen)
  - Click "Purchase Volume: 50%" in the lower right corner of the game screen
5. Target the flash game subscreen with SCAR, dragging the "Crosshair" icon
   onto the game flash screen in the browser window
  - WARNING! Whenever you move the browser window OR scroll the page, 
    you should re-target the game screen with the "Crosshare" tool in SCAR
  - The game has now got an "autofocus" feature that tries to automatically
    target the game screen and resize the page to 100% scale.
6. Launch the bot ("Play" button in SCAR or press Ctrl+Alt+R). 
  - WARNING! The bot will stop working if the "Price" column is blocked by
    achievement panels. Watch them from time to time and close them manually!
  - You can play the game as usual, but bot will act only when The Tangerine
    Exchange screen is opened.
  - You can use another browser window for browsing while bot is playing. 
  - You can work with other programs as well, even blocking the game
    screen with other window. The bot will shortly activate the game window 
    from time to time to press an appropriate button in the game.
    It will appear as if the browser window will "flicker" for a moment.
7. Whenever you feel to stop the bot playing, press "Stop" in SCAR. 
   (Global shortcut: Ctrl+Alt+S)

Troubleshooting
===============================
- If the bot stopped working, try to re-target the game window, dragging
  the "Crosshair" tool from SCAR toolpanel to the flash screen of the game.
- Please, ensure the page scale is set to 100%. The bot will not recognise
  the fonts of the game otherwise.
- Bot can work properly only when the "Price" and the "Buy/Sell" columns of the
  Exchange screen are visible
- If bot strangely "jerks" the mouse cursor way too often, ensure
  that the "Purchase Volume" on the Exchange screen is set to 50% (NOT 100%)  

Future improvements
===============================
- TODO: Tutorial with pictures
- TODO: Add mode to quickly unlock 100 universes. 
  - Every 5 minutes go to main screen and try to buy the last not bought item
  - Stop when successfully bought The Tangerine God 
  - => go to achievement screen and press Breach the Universe
  - => skip the cutscene, press 1000 times on a tangerine, buy a couple of
     structures, go to The Tangerine Exchange screen
- TODO: If buying 5th resource, spend all the money
- TODO: Auto switch to 50% purchase Volume at the start
- TODO: Go to the Exchange screen from any screen at the start
- TODO: think of advanced strategies of spending 100% resources on lowest resource
- NOTE: Using Hinting a does not help much! :(
  - The only usefull feature: '+' goes to '-' only when it's >= 80
  - Anyway, it's kinda bullshit, not gonna implement anything about it
*)

program TangerineStockBot; 
var 
  buyBmp, sellBmp, twoBmp, eightBmp: TSCARBitmap; 
   
// Initialize bitmaps for searching
procedure LoadBitmaps; begin
  buyBmp := TSCARBitmap.Create('deNrjYOBk+A8GG7dfTclfc+7SUwi3a/IBZPF5S' +
    '09DuK/ffkEWz63Y+B8VQMSB6MjJ+1jF4SZDuEBU3rgNKAtEaOJwlUAANA1T/fJ' +
    '1F+CCmAgAMZegNQ==');          
  sellBmp := TSCARBitmap.Create('deNoTZmBn+A8GN26/QkYp+WuAJESqa/KBjduvQ' +
    'thABlAKgiAi5Y3bgOzl6y4ApSCyRGp8+OQ9EEHYR07eJ0YjXBBuQm7FRmI0AlF' +
    'j126gLBBBHDxoEQAI1QtH');      
  twoBmp := TSCARBitmap.Create('deNpjZeBk+I8Evn77+R8DlDduS8lfA5HauP0qk' +
    'A1ED5+8B3Ih7CMn70NU5lZsPHfpKYR94/YriBq4LrgVQCkgQrMFAFl9dlg=');
  eightBmp := TSCARBitmap.Create('deNpjZeBguHH71X8MABSEiz988j4lf01uxUa47' +
    'JGT93cfuAVkvH775T9eADSkvHHbxu1X4RrhRgEAH6Vr+Q==');            
end;  


procedure clickAndReturn(x, y: integer);
var w: Hwnd;
    client: TSCARClient;
    a, b: integer;
begin
  w := GetActiveWindow; // get current window in focus
  client := GetClient;
  GetMousePos(a, b);
  client.update; //?
  client.activate;
  
  sleep(50); // time for switching the window
  
  ClickMouse(x, y, mbLeft);
  sleep(50); 
  SetMousePos(a, b);
  ActivateWindow(w);
   
end;

procedure tryToTrade; 
var i: integer;    
    x, y, a, b: integer;
begin

  for i := 0 to 4 do begin     
  
    // - Search for the strings with the word "Buy"
    // buyBmp at 705, 120, 724, 134
    // --, +30, --, +30  
    if FindBitmapTol(x, y, buyBmp, 705, 120 + i * 30, 724, 134 + i * 30, 10) then begin
      // -- Find the ones with the '2' in the beginning
      // oneBmp at 272, 115, 285, 130
      // --, +30, --, +30    
      //write('Found buy at'); writeln(i);
      if FindBitmapTol(a, b, twoBmp, 272, 115 + i * 30, 285, 130 + i * 30, 50) then begin 
        Write(DateToStr(Date)); write(' '); Write(TimeToStr(Time)); 
        write(': Buying at '); writeln(i + 1);
        // -- Press 'Buy' on them
        // clickMouse at the found coordinates of "Buy"       
        clickAndReturn(x, y);
      end;                    
    end;    
    
    // - Search for the strings with the word "Sell"
    // sellBmp at 701, 115, 727, 130
    // --, +30, --, +30  
    if FindBitmapTol(x, y, sellBmp, 701, 115 + i * 30, 727, 130 + i * 30, 10) then begin  
      // -- Find the ones with the '8' in the beginning
      // eightBmp at 252, 115, 266, 130
      // --, +30, --, +30        
      //write('Found sell at'); writeln(i);
      if FindBitmapTol(a, b, eightBmp, 252, 115 + i * 30, 266, 130 + i * 30, 50) then begin
        Write(DateToStr(Date)); write(' '); Write(TimeToStr(Time)); 
        write(': Selling at '); writeln(i + 1);
        // -- Press 'Sell' on them
        // clickMouse at the found coordinates of "Sell"                          
        clickAndReturn(x, y);
      end;
    end;
             
  end;

end;



function tryToTarget: boolean;
  var
    wnds, wnds2: THwndArray;  
    i, j, width: integer;       
    client, outer: TSCARWindowClient; 
    proportion: extended;
begin
  // Get the Firefox window with activated Tangerine Tycoon tab
  Wnds := FindWindowsEx(GetDesktopWindow, 'Tangerine Tycoon', '', False, False, True);
  // Try to get the flash children of the found windows
  for i := low(wnds) to high(wnds) do begin
    Wnds2 := FindWindowsEx(wnds[i], '', 'GeckoFPSandboxChildWindow', True, False, True);
    for j := low(wnds2) to high(wnds2) do begin 
      client := TSCARWindowClient.Create(Wnds2[j]) 
      proportion := client.ImageArea.x2 / client.imagearea.y2;
      // Proportion should be 800/600(100%)
      if (proportion >= 1.324) and (proportion <= 1.344) then begin
        width := client.ImageArea.x2; 
        if (width > 805) or (width < 795) then begin
          // Focus on outer window, enlarge or shrink the web page 
          outer := TSCARWindowClient.create(getouterwindow(client.handle));
          SetClient(outer)       
          outer.activate;
          while (width > 805) do begin 
            // press Ctrl+Minus  
            writeln('Shrinking the game page'); 
            sleep(100);
            vkeydown(17); // CTRL
            vkeydown(109); // '-'   
            sleep(10);
            vkeyup(109);
            vkeyup(17); 
            sleep(2000);
            client.update;
            sleep(50);
            width := client.ImageArea.x2;
          end;
          while width < 795 do begin
            // press Ctrl+Plus              
            writeln('Enlarging the game page'); 
            sleep(100);
            vkeydown(17); // CTRL
            vkeydown(107); // '+'   
            sleep(10);
            vkeyup(107);
            vkeyup(17);     
            sleep(2000);
            client.update;
            sleep(50);
            width := client.ImageArea.x2;
          end;    
          outer.free;
        end; 
        
        SetClient(client);
        client.activate; 
        //client.free; // for some strage reason it causes Access violation
        result := true;  
        exit; // from function    
      end;   
      
      client.free;
    end;
  end;       

end;


// "Main" function
begin
  LoadBitmaps;   
  
  if not tryToTarget then begin
    Writeln('Unable to target the game automatically, please do it manually!')
  end;
  
  while(True) do begin
    tryToTrade;
    Sleep(1000);
  end;

end.
